get_filename_component(CURRENT_FOLDER_NAME "${CMAKE_CURRENT_SOURCE_DIR}" NAME)

# Find Intel MKL via vcpkg
find_package(MKL REQUIRED)
get_target_property(MKL_INCLUDE_DIRS MKL::MKL INTERFACE_INCLUDE_DIRECTORIES)
message(STATUS "MKL INTERFACE_INCLUDE_DIRECTORIES: ${MKL_INCLUDE_DIRS}")

add_library(${CURRENT_FOLDER_NAME} SHARED)
target_compile_definitions(${CURRENT_FOLDER_NAME} PRIVATE BASE_BUILD)
target_link_libraries(${CURRENT_FOLDER_NAME} PRIVATE MKL::MKL)

include(FetchContent)
FetchContent_Declare(
    nanobind
    GIT_REPOSITORY https://github.com/wjakob/nanobind.git
    GIT_TAG v2.9.2
    GIT_SHALLOW TRUE
    GIT_PROGRESS TRUE
)
FetchContent_MakeAvailable(nanobind)

file(GLOB_RECURSE PROJECT_SOURCES CONFIGURE_DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.c"
)
target_sources(${CURRENT_FOLDER_NAME} PRIVATE ${PROJECT_SOURCES})

file(GLOB_RECURSE INTERNAL_HEADERS CONFIGURE_DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/*.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/*.hpp"
)
list(REMOVE_ITEM INTERNAL_HEADERS
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/${CURRENT_FOLDER_NAME}.h"
)
target_sources(${CURRENT_FOLDER_NAME} PRIVATE ${INTERNAL_HEADERS})

target_include_directories(${CURRENT_FOLDER_NAME}
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:include>
)

target_include_directories(${CURRENT_FOLDER_NAME} INTERFACE ${MKL_INCLUDE_DIRS})

target_precompile_headers(${CURRENT_FOLDER_NAME} 
    PRIVATE 
        ${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/pch.h)

set_target_properties(${CURRENT_FOLDER_NAME} PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${LIBS_PATH}"
    PREFIX ""
)

# install(FILES
#     ${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/${CURRENT_FOLDER_NAME}.h
#     DESTINATION include/${CURRENT_FOLDER_NAME}
#     COMPONENT development
# )

install(TARGETS ${CURRENT_FOLDER_NAME}
    LIBRARY DESTINATION "${LIBS_PATH}"
    RUNTIME DESTINATION "${LIBS_PATH}"
    ARCHIVE DESTINATION "${LIBS_PATH}"
)
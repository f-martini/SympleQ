get_filename_component(CURRENT_FOLDER_NAME "${CMAKE_CURRENT_SOURCE_DIR}" NAME)

add_library(${CURRENT_FOLDER_NAME} SHARED)
target_compile_definitions(${CURRENT_FOLDER_NAME} PRIVATE MEASUREMENT_BUILD)

file(GLOB_RECURSE PROJECT_SOURCES CONFIGURE_DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/*.c"
)
target_sources(${CURRENT_FOLDER_NAME} PRIVATE ${PROJECT_SOURCES})

file(GLOB_RECURSE INTERNAL_HEADERS CONFIGURE_DEPENDS
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/*.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/*.hpp"
)
list(REMOVE_ITEM INTERNAL_HEADERS
    "${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/${CURRENT_FOLDER_NAME}.h"
)
target_sources(${CURRENT_FOLDER_NAME} PRIVATE ${INTERNAL_HEADERS})

target_include_directories(${CURRENT_FOLDER_NAME}
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:include>
)

target_precompile_headers(${CURRENT_FOLDER_NAME} 
PRIVATE 
${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/pch.h)

target_link_libraries(${CURRENT_FOLDER_NAME} PRIVATE Base)

set_target_properties(${CURRENT_FOLDER_NAME} PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${LIBS_PATH}"
    PREFIX ""
)

# install(FILES
#     ${CMAKE_CURRENT_SOURCE_DIR}/include/${CURRENT_FOLDER_NAME}/${CURRENT_FOLDER_NAME}.h
#     DESTINATION include/${CURRENT_FOLDER_NAME}
#     COMPONENT development
# )

install(TARGETS ${CURRENT_FOLDER_NAME}
    LIBRARY DESTINATION "${LIBS_PATH}"
    RUNTIME DESTINATION "${LIBS_PATH}"
    ARCHIVE DESTINATION "${LIBS_PATH}"
    COMPONENT python_modules
)
